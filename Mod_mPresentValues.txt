Attribute VB_Name = "mPresentValues"
Public Function Act_ax_k(Age As Integer, Sex As String, TableId As String, InterestRate As Double, k As Integer, Optional BirthYear As Integer, Optional RetirementAge As Integer, Optional Layer As Integer = 1) As Double
    If k > 0 Then
        Act_ax_k = Act_Nx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) / _
                   Act_Dx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) - _
                   Act_DeductionTerm(k, InterestRate)
    Else
        Act_ax_k = 0
    End If
End Function

Public Function Act_axn_k(Age As Integer, n As Integer, Sex As String, TableId As String, InterestRate As Double, k As Integer, Optional BirthYear As Integer, Optional RetirementAge As Integer, Optional Layer As Integer = 1) As Double
    If k > 0 Then
        Act_axn_k = (Act_Nx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) - _
                     Act_Nx(Age + n, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer)) / _
                    Act_Dx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) - _
                    Act_DeductionTerm(k, InterestRate) * (1 - _
                    Act_Dx(Age + n, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) / _
                    Act_Dx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer))
    Else
        Act_axn_k = 0
    End If
End Function

Public Function Act_nax_k(Age As Integer, n As Integer, Sex As String, TableId As String, InterestRate As Double, k As Integer, Optional BirthYear As Integer, Optional RetirementAge As Integer, Optional Layer As Integer = 1) As Double
    If k > 0 Then
        Act_nax_k = Act_Dx(Age + n, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) / _
                    Act_Dx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) * _
                    Act_ax_k(Age + n, Sex, TableId, InterestRate, k, BirthYear, RetirementAge, Layer)
    Else
        Act_nax_k = 0
    End If
End Function

Public Function act_nGrAx(Age As Integer, n As Integer, Sex As String, TableId As String, InterestRate As Double, Optional BirthYear As Integer, Optional RetirementAge As Integer, Optional Layer As Integer = 1) As Double
    act_nGrAx = (Act_Mx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) - _
                 Act_Mx(Age + n, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer)) / _
                Act_Dx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer)
End Function

Public Function act_nGrEx(Age As Integer, n As Integer, Sex As String, TableId As String, InterestRate As Double, Optional BirthYear As Integer, Optional RetirementAge As Integer, Optional Layer As Integer = 1) As Double
    act_nGrEx = Act_Dx(Age + n, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer) / _
                Act_Dx(Age, Sex, TableId, InterestRate, BirthYear, RetirementAge, Layer)
End Function

Public Function Act_ag_k(g As Integer, InterestRate As Double, k As Integer) As Double
    Dim v As Double
    v = 1 / (1 + InterestRate)
    
    If k > 0 Then
        If InterestRate > 0 Then
            Act_ag_k = (1 - v ^ g) / (1 - v) - Act_DeductionTerm(k, InterestRate) * (1 - v ^ g)
        Else
            Act_ag_k = g
        End If
    Else
        Act_ag_k = 0
    End If
End Function


Public Function Act_DeductionTerm(k As Integer, InterestRate As Double) As Double
    ' Deduction term
    Dim l As Integer
    Act_DeductionTerm = 0
    
    If k > 0 Then
        For l = 0 To k - 1
            Act_DeductionTerm = Act_DeductionTerm + l / k / (1 + l / k * InterestRate)
        Next l
        Act_DeductionTerm = Act_DeductionTerm * (1 + InterestRate) / k
    End If
End Function

